//-- Author: Saswata Paul
//-- An ontology to represent hazrad analysis for the FAA AI/ML Certification Project

 uri "http://sadl.org/FHA.sadl" alias fha.
 import "http://arcos.rack/PROV-S".
 import "http://arcos.rack/DOCUMENT".
 import "http://arcos.rack/SYSTEM".
 import "http://arcos.rack/SECURITY".
 import "http://arcos.rack/SOFTWARE".
 import "http://arcos.rack/HARDWARE".
 import "http://arcos.rack/REQUIREMENTS".
 import "http://arcos.rack/REVIEW".
 import "http://arcos.rack/TESTING".
 import "http://arcos.rack/HAZARD".
 import "http://arcos.rack/ANALYSIS".
 import "http://arcos.rack/PROCESS".
 import "http://arcos.rack/AGENTS".
 import "http://arcos.rack/CONFIDENCE".
 
 //---------------------------------------------------------------------------------------------
 //-- Additional classes required related to Hazards
 //--------------------------------------------------------------------------------------------- 
 Severity (note "Severity Types taken from FAA Circular AC No: 23.1309-lE") is a type of THING, 
 	must be one of {Negligible (note "No safety effect: failure conditions that would not affect the operational capability of the airplane or increase crew workload"), 
 					Minor (note "Failure conditions that would not significantly reduce airplane safety and involve crew actions that are within their capabilities."), 
 					Major (note "Failure conditions that would reduce the capability of the airplane or the ability of the crew to cope with adverse operating conditions to the extent that there would be a significant reduction in safety margins or functional capabilities."), 
 					Hazardous (note "Failure conditions that would reduce the capability of the airplane or the ability of the crew to cope with adverse operating conditions"), 
 					Catastrophic (note "Failure conditions that are expected to result in multiple fatalities of the occupants, or incapacitation or fatal injury to a flight crewmember normally with the loss of the airplane.")}.
 
 DesignAssuranceLevel (note "The minimum Design Assurance Level") is a type of THING
 	must be one of {LevelA (note "Level A"),  // Cannot use 'A' since it is a keyword in SADL, so using LevelA instead
 					LevelB (note "Level B"), 
 					LevelC (note "Level C"), 
 					LevelD (note "Level D")}.
 					
 VerificationMethod (note "The method used for verifying a component") is a type of ACTIVITY
 	must be one of {FHA (note "Functional Hazard Assessment"),
					FTA (note "Fault Tree Analysis"),
					DD (note "Dependency Diagram"),
					MA (note "Markov Analysis"),
					FMEA (note "Failure Modes and Effects Analysis"),
					FMES (note "Failure Modes and Effects Summary"),
					ZSA (note "Zonal Safety Analysis"),
					CMA (note "Common Mode Analysis"),
					PRA (note "Particular Risk Analysis")}.
					 
 Phase (note "The flight phase") is a type of THING
 	must be one of {ALL (note "All phases"),
 					STD (note "Standing"),
 					PBT (note "Pushback/Towing"),
 					TXI (note "Taxi"),
 					TOF (note "TakeOff"),
 					ICL (note "Initial climb"), 
 					ENR (note "En-route (Cruise)"),
 					APR (note "Approach"),
 					LDG (note "Landing")}.


 //--------------------------------------------------------------------------------------------- 	
 //-- Additional properties required related to Hazards
 //--------------------------------------------------------------------------------------------- 
 
 //-- For HAZARD 	
 eventPhase (note "The event phase") describes HAZARD with values of type Phase. 
 severityClassification (note "The severity classification of the hazard") describes HAZARD with values of type Severity. // Required because the original "severity" property of HAZARD is float [0,1]
 minimumRequiredDal (note "The minimal DAL required for such a hazard") describes HAZARD with values of type DesignAssuranceLevel.
 classificationJustification (note "Justification of severity classification") describes HAZARD with values of type string.
 verificationMethod (note "The verification method used to verify that the hazard has been mitigated") describes HAZARD with values of type VerificationMethod.
 affects (note "The system affected by the hazard") describes HAZARD with values of type SYSTEM.
 	
 
 
 //---------------------------------------------------------------------------------------------
 //-- Component level safety assessment
 //--------------------------------------------------------------------------------------------- 
 
 NNFailureMode is a type of HAZARD
 	described by name with values of type string
 	described by failureModeType with values of type FailureModeType
 	described by contributesTo with values of type HAZARD
 	described by mitigatedBy with values of type REQUIREMENT. // is this needed?
 	
 FailureModeType is a type of THING
 	must be one of {performanceBias, 
 					outOfDistribution, 
 					trainingProductionSkew, 
 					robustnessFailure, 
 					targetLeakage, 
 					dataDrift, 
 					automatedNaivete, 
 					cascadingModelFailure, 
 					rewardHacking}.
 